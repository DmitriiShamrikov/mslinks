plugins {
    id 'java-library'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'eclipse'

    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = "org.jabref"
version = '1.1.0-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    // Ignore warnings because of missing elements
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

test {
    useJUnit {
        excludeCategories = ['mslinks.ReadTestData', 'mslinks.WriteTestData', 'mslinks.Examples']
    }
}

testlogger {
	theme = 'mocha'
	showStandardStreams = true
}

eclipse {
    classpath {
        defaultOutputDir = file('build/eclipse-default')
        file.whenMerged {
            entries.each { entry ->
                if (entry.kind == 'src' && entry.hasProperty('output')) {
                    entry.output = entry.output.replace('bin/', "build/eclipse/")
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mslinks'
            from components.java
            pom {
                name = 'mslinks'
                description = 'Library for parsing and creating Windows shortcut files (.lnk)'
                url = 'https://github.com/JabRef/mslinks'

                licenses {
                    license {
                        name = 'WTFPL'
                        url = 'http://www.wtfpl.net/about/'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'DmitriiShamrikov'
                    }
                }

                scm {
                    url = 'https://github.com/JabRef/mslinks'
                    connection = 'scm:git:git://github.com/JabRef/mslinks.git'
                    developerConnection = 'scm:git:git@github.com:JabRef/mslinks.git'
                }
            }
        }
    }
}

signing {
    required = isReleaseVersion
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_TOKEN")
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}
